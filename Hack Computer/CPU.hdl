// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Central Processing unit (CPU).
 * Consists of an ALU and a set of registers, designed to fetch and 
 * execute instructions written in the Hack machine language.
 * In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM=0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time unit. If reset=1 then the 
 * CPU jumps to address 0 (i.e. sets pc=0 in next time unit) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset=1) or continue executing
                         // the current program (reset=0).

    OUT outM[16],        // M value output
        writeM,          // Write into M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    // Implement the CPU based on its following functionalities


    // Instruction decode


    // A register and input mux

    
    // D register
    

    // ALU and input mux
 

    // PC with jump test

    // identify type of instruction
    Not(in=instruction[15], out=Ainstruction);
    Not(in=Ainstruction, out=Cinstruction);

    // A register
    // if C instruction and store at register A is true
    And(a=Cinstruction, b=instruction[5], out=ALUtoa); 
    Mux16(a=instruction, b=ALUout, sel=ALUtoa, out=regA);
    // load A if is A instruction or C instruction that destination to register A
    Or(a=Ainstruction, b=ALUtoa, out=loadA);
    ARegister(in=regA, load=loadA, out=RegAout);

    // D register
    And(a=Cinstruction, b=instruction[4], out=loadD);
    DRegister(in=ALUout, load=loadD, out=RegDout);

    Mux16(a=RegAout, b=inM, sel=instruction[12], out=AMout);   // select A or M based on a-bit


    ALU(x=RegDout, y=AMout,
    zx=instruction[11],
    nx=instruction[10],
    zy=instruction[9], 
    ny=instruction[8], 
    f=instruction[7], 
    no=instruction[6], out=ALUout, zr=  zrout, ng=ngout);

    Or16(a=false, b=ALUout, out=outM);
    Or16(a=false, b=RegAout, out=addressM);
    And(a=Cinstruction, b=instruction[3], out=writeM);

    // PC
    And(a=zrout, b=instruction[1], out=jeq);
    And(a=ngout, b=instruction[2], out=jlt);
    // check if output is positive
    Or(a=zrout, b=ngout, out=zeroOrneg);
    Not(in=zeroOrneg, out=positive);
    And(a=positive, b=instruction[0], out=jgt);

    Or(a=jeq, b=jlt, out=jle);
    Or(a=jle, b=jgt, out=jump);

    And(a=Cinstruction, b=jump, out=loadPC);
    Not(in=loadPC, out=incPC);
    PC(in=RegAout, load=loadPC, inc=incPC, reset=reset, out[0..14]=pc);
   
}
